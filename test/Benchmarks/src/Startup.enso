from Standard.Base import all

from Standard.Test import Bench

polyglot java import java.lang.System as Java_System
polyglot java import java.io.File as Java_File

type Data
    Value ~enso_bin:File ~empty_world:File ~hello_world:File

    bench_empty self =
        startup self.enso_bin.path [ "--run", self.empty_world.to_text ]

    bench_hello self =
        startup self.enso_bin.path [ "--run", self.hello_world.to_text ]

startup exe:Text args:(Vector Text) =
    result = Process.run exe args
    case result.exit_code of
        Exit_Code.Failure code ->
            IO.println "Exit code: "+code
        Exit_Code.Success -> Nothing
    IO.println result.stdout
    IO.println result.stderr

collect_benches = Bench.build builder->
    options = Bench.options . set_warmup (Bench.phase_conf 2 5) . set_measure (Bench.phase_conf 3 5)


    data =
        Data.Value enso_bin empty_file hello_file

    builder.group "Startup" options group_builder->
        group_builder.specify "empty_startup" data.bench_empty
        group_builder.specify "hello_world_startup" data.bench_hello

empty_file =
    f = File.create_temporary_file "empty" "enso"
    t = """
        main = "Hello World"
    t.write f
    f

hello_file =
    f = File.create_temporary_file "hello" "enso"
    t = """
        from Standard.Base import IO

        main = IO.println "Hello World"
    t.write f
    f

enso_bin =
    find_prefix dir prefix =
        vec = dir.list name_filter=prefix+"*"
        if vec.length == 1 then vec.at 0 else
            msg = "Cannot find " + prefix + "* in" + dir.to_text + "\n"
            err = dir.list.fold msg t-> f->
                t + f.to_text + "\n"
            Panic.throw err

    project_root = File.new enso_project.root.to_text
    repository_root = project_root . parent . parent
    built_distribution = find_prefix repository_root "built-distribution"
    enso_engine = find_prefix built_distribution "enso-engine-"
    enso = find_prefix enso_engine "enso-"
    bin = find_prefix enso "bin"

    exe = File.new bin / if Platform.os == Platform.OS.Windows then "enso.bat" else "enso"

    if exe.is_regular_file.not then Panic.throw "Cannot find "+exe.to_text

    exe

main = collect_benches . run_main

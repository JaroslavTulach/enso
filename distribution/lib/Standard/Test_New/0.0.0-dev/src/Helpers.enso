private

from Standard.Base import all
from Standard.Base.Runtime import State

import project.Clue.Clue
import project.Group.Group
import project.Spec_Result.Spec_Result
import project.Spec.Spec
import project.Suite.Suite
import project.Suite_Config.Suite_Config
import project.Test_Result.Test_Result
import project.Test_Reporter
import project.Test.Test

run_group_with_filter : Group -> Regex -> Vector Test_Result
run_group_with_filter (group : Group) (spec_filter : Regex) =
    filtered_specs = group.specs.filter spec->
        (spec_filter.match spec.name) != Nothing
    run_specs_from_group filtered_specs group


run_group : Group -> Vector Test_Result
run_group (group : Group) =
    run_specs_from_group group.specs group


run_specs_from_group : Vector Spec -> Text -> Vector Test_Result
run_specs_from_group (specs : Vector Spec) (group : Group) =
    case specs.is_empty of
        True -> []
        False ->
            IO.println <| "Running tests in group '" + group.name + "'..."
            setup_args = group.setup Nothing
            test_results = specs.map spec->
                IO.println <| "  Running spec '" + spec.name + "'..."
                pair = run_spec spec setup_args
                IO.println <| "  Finished spec '" + spec.name + "'."
                spec_res = pair.second
                time_taken = pair.first
                Test_Result.Impl group.name spec.name spec_res time_taken
            group.teardown setup_args
            test_results

## PRIVATE
   Runs a single test spec.

   Arguments:
   - args : Arguments from Group's setup method.
run_spec : Spec -> Any -> Pair Duration Spec_Result
run_spec (spec : Spec) (args : Any) =
    pair = case spec.pending of
        Nothing ->
            Duration.time_execution <|
                State.run Clue Nothing (execute_spec_code spec.code args)
        reason -> Pair.new Duration.zero (Spec_Result.Pending reason)
    pair


## PRIVATE
execute_spec_code : (Nothing -> Any) -> Any -> Spec_Result
execute_spec_code spec_code spec_args =
    recovery = Panic.recover Any <|
        result = spec_code spec_args
        result.catch Any err->
            Panic.throw (Finished_With.Error err result.get_stack_trace_text)
        Nothing
    maybeExc = case recovery of
        _ -> Spec_Result.Success
    result = maybeExc.catch Any ex->
        case ex of
            Spec_Result.Failure _ _ -> ex
            Finished_With.Error err stack_trace_text ->
                Spec_Result.Failure (Test.enrich_message_with_clue ("An unexpected error was returned: " + err.to_text)) details=stack_trace_text
            _ -> Spec_Result.Failure (Test.enrich_message_with_clue ("An unexpected panic was thrown: " + ex.to_text)) details=maybeExc.get_stack_trace_text
    result

## PRIVATE

   An error describing that a test finished with an unexpected error.
type Finished_With
    ## PRIVATE

       An error describing that a test finished with an unexpected error.

       Arguments:
       - err: The payload of the error that triggered this error.
       - stack_trace_text: A textual representation of the stack trace for the
         error.
    Error err stack_trace_text
